buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath 'com.eriwen:gradle-css-plugin:1.1.2'
    }
}
buildscript {
    configurations.classpath.resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

repositories {
    mavenLocal()
    mavenCentral()
}

apply plugin: 'css'

css.source {
    custom {
        css {
            srcDir "src/test/resources"
            include "file*.css"
        }
    }
}

task clean(type: Delete) {
    delete buildDir
}

// Combine CSS files
task combine(type: com.eriwen.gradle.css.tasks.CombineCssTask) {
    source = css.source.custom.css.files
    dest = "${buildDir}/all.css"
}

// Minify with YUI Compressor
task minify(type: com.eriwen.gradle.css.tasks.MinifyCssTask) {
    source = combine
    dest = file("${buildDir}/all-min.css")
    yuicompressor {
        lineBreakPos = 100
    }
}

// GZip it!
task gzip(type: com.eriwen.gradle.css.tasks.GzipCssTask) {
    source = minify
    dest = "${buildDir}/all-zipped.min.css"
}

csslint {
    source = css.source.custom.css.files
    dest = "${buildDir}/csslint.out"
    options {
        format = 'text'
        warnings = ['box-model', 'ids']
        errors = []
    }
}

kss {
    source = css.source.custom.css.files
    sourceDir = file("src/test/resources")
    dest = file("${buildDir}/kss.out")
}
