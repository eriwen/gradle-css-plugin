plugins {
    id "com.gradle.plugin-publish" version "0.10.1"
    id "com.jfrog.bintray" version "1.8.4"
}

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'idea'
apply plugin: 'jacoco'

sourceCompatibility = 1.7
targetCompatibility = 1.7

defaultTasks 'clean', 'build'

version = '2.14.0'
group = 'com.eriwen'
ext.artifact = 'gradle-css-plugin'
ext.isSnapshot = version.endsWith("-SNAPSHOT")

repositories {
    jcenter()
    mavenCentral()
}

task createClasspathManifest {
    def outputDir = sourceSets.test.output.resourcesDir

    inputs.files sourceSets.main.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
    }
}

dependencies {
    compile gradleApi()
    compile('com.yahoo.platform.yui:yuicompressor:2.4.8') {
        exclude module: 'junit'
    }
    compile 'com.asual.lesscss:lesscss-engine:1.3.3'
    testCompile gradleTestKit()
    testCompile files(createClasspathManifest)
    testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude module: 'junit-dep'
        exclude module: 'groovy-all'
    }
    testCompile 'commons-lang:commons-lang:2.6'
}

idea.module {
    ext.gradleCacheVariable = 'GRADLE_CACHE'
    ext.downloadJavadoc = true
    ext.downloadSource = true
    outputDir = file('intellij/out')
    testOutputDir = file('intellij/testOut')
}

idea.project.ipr.withXml { provider ->
    def node = provider.asNode()
    // Use git
    def vcsConfig = node.component.find { it.'@name' == 'VcsDirectoryMappings' }
    vcsConfig.mapping[0].'@vcs' = 'Git'

    // Set gradle home
    def gradleSettings = node.appendNode('component', [name: 'GradleSettings'])
    gradleSettings.appendNode('option', [name: 'SDK_HOME', value: gradle.gradleHomeDir.absolutePath])

    // Add gradle IDE support
    def gradleUISettings = node.appendNode('component', [name: 'GradleUISettings'])
    gradleUISettings.appendNode('setting', [name: 'root'])

    def gradleUISettings2 = node.appendNode('component', [name: 'GradleUISettings2'])
    gradleUISettings2.appendNode('setting', [name: 'root'])
}

compileGroovy {
    options.compilerArgs = ['-Xlint:deprecation', '-Xlint:unchecked']
}

task sourceJar(type: Jar) {
    description = 'An archive of the source code for Maven Central'
    classifier = 'sources'
    from sourceSets.main.groovy
}

task groovydocJar(type: Jar) {
    description = 'An archive of the GroovyDocs for Maven Central'
    classifier = 'javadoc'
    from groovydoc
}

artifacts {
    archives groovydocJar, sourceJar
}

publishing {
    publications {
        maven(MavenPublication) {
            if (plugins.hasPlugin('war')) {
                from components.web
            } else {
                from components.java
            }

            artifact sourceJar {
                classifier 'sources'
            }

            artifact groovydocJar {
                classifier 'javadoc'
            }
        }
    }
}

pluginBundle {
    website = 'https://github.com/eriwen/gradle-css-plugin/'
    vcsUrl = 'https://github.com/eriwen/gradle-css-plugin/'
    description = 'Gradle plugin for working with CSS'
    tags = ['css', 'less', 'csslint']

    plugins {
        jsPlugin {
            id = 'com.eriwen.gradle.css'
            displayName = 'Gradle CSS Plugin'
        }
    }
    mavenCoordinates {
        groupId = 'com.eriwen'
        artifactId = 'gradle-css-plugin'
    }
}

signing {
    sign configurations.archives
}

bintray {
    user = System.properties['bintrayUsername']
    key = System.properties['bintrayApiKey']
    publications = ['maven']
    pkg {
        repo = 'gradle-plugins'
        name = 'gradle-css-plugin'
        desc = 'Gradle plugin for working with CSS.'
        licenses = ['Apache-2.0']
        labels = ['gradle', 'css']
    }
}

test {
    systemProperties['version'] = version
    testLogging {
        stackTraceFilters 'truncate', 'groovy'
        events 'passed', 'skipped', 'failed'
        exceptionFormat = 'full'
    }
}

// Only *require* signing if we are uploading a non snapshot version
gradle.taskGraph.whenReady { taskGraph ->
    tasks.withType(org.gradle.plugins.signing.Sign).all {
        required = taskGraph.hasTask(":uploadArchives") && !isSnapshot
    }
}

install.repositories.mavenInstaller {
    pom.project(pomConfiguration)
    if (signing.signatory) {
        beforeDeployment { signing.signPom(it) }
    }
}

uploadArchives {
    repositories.mavenDeployer {
        if (signing.signatory) {
            beforeDeployment { signing.signPom(it) }
        }
        name = 'mavenCentralReleaseDeployer'
        repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
            authentication(userName: System.properties['mavenCentralUsername'], password: System.properties['mavenCentralPassword'])
            releases(updatePolicy: 'always')
            snapshots(updatePolicy: 'always')
        }
        pom.project(pomConfiguration)
    }
}

/**
 * Create POM config and return for use by other tasks.
 */
def getPomConfiguration() {
    return {
        name 'Gradle CSS Plugin'
        packaging 'jar'
        description 'A Gradle plugin for working with CSS.'
        url 'https://github.com/eriwen/gradle-css-plugin'
        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }
        developers {
            developer {
                id 'eriwen'
                name 'Eric Wendelin'
                email 'me@eriwen.com'
            }
        }
        scm {
            connection 'scm:https://eriwen@github.com/eriwen/gradle-css-plugin'
            developerConnection 'scm:git@github.com:eriwen/gradle-css-plugin.git'
            url 'https://github.com/eriwen/gradle-css-plugin'
        }
    }
}

// download dependencies all at once, keeps downloads out of travis output
task resolveAllDependencies {
    doLast {
        configurations.all { configuration ->
            if (configuration.isCanBeResolved()) {
                println "Resolving $configuration"
                configuration.resolve()
            }
            else {
                println "$configuration is not resolvable"
            }
        }
    }
}
